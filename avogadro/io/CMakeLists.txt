find_package(Eigen3 REQUIRED)
find_package(Boost REQUIRED)
# Add as "system headers" to avoid warnings generated by them with
# compilers that support that notion.
include_directories(SYSTEM "${EIGEN3_INCLUDE_DIR}"
  "${Boost_INCLUDE_DIRS}"
  "${AvogadroLibs_SOURCE_DIR}/thirdparty/rapidxml")

set(HEADERS
  cmlformat.h
)

set(SOURCES
  cmlformat.cpp
)

# Ever feel like you should just take the modular build system?
include_directories("${CMAKE_CURRENT_BINARY_DIR}/../core")

add_library(AvogadroIO ${HEADERS} ${SOURCES})
target_link_libraries(AvogadroIO AvogadroCore)

# Generate an export header for the AvogadroCore library. This handles symbol
# visibility on all platforms using functionality added in CMake 2.8.6.
include(GenerateExportHeader)
generate_export_header(AvogadroIO EXPORT_FILE_NAME avogadroio.h)
list(APPEND HEADERS "${CMAKE_CURRENT_BINARY_DIR}/avogadroio.h")
include_directories(${CMAKE_CURRENT_BINARY_DIR})
add_compiler_export_flags(avogadro_export_flags)
set_property(TARGET AvogadroIO APPEND
  PROPERTY COMPILE_FLAGS ${avogadro_export_flags})
install(FILES ${HEADERS} DESTINATION "${INSTALL_INCLUDE_DIR}/avogadro/io")
install(TARGETS AvogadroIO
  RUNTIME DESTINATION "${INSTALL_RUNTIME_DIR}"
  LIBRARY DESTINATION "${INSTALL_LIBRARY_DIR}"
  ARCHIVE DESTINATION "${INSTALL_ARCHIVE_DIR}")
