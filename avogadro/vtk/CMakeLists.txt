find_package(Eigen3 REQUIRED)
# Add as "system headers" to avoid warnings generated by them with
# compilers that support that notion.
include_directories(SYSTEM ${EIGEN3_INCLUDE_DIR})
find_package(OpenGL REQUIRED)
find_package(GLEW REQUIRED)
include_directories(SYSTEM ${OPENGL_INCLUDE_DIR} ${GLEW_INCLUDE_DIRS})
if(WIN32 AND NOT BUILD_SHARED_LIBS)
  add_definitions(-DGLEW_STATIC)
endif()

find_package(Qt5 COMPONENTS Widgets REQUIRED)

find_package(VTK QUIET REQUIRED)
if(VTK_MAJOR_VERSION GREATER 8)
  set(VCP "") # shorthand for VTK_COMPONENT_PREFIX
  set(VMP "VTK::") # shorthand for VTK_MODULE_PREFIX
else()
  set(VCP "vtk")
  set(VMP "vtk")
  include_directories(SYSTEM ${VTK_INCLUDE_DIRS})
endif()

find_package(VTK
  REQUIRED COMPONENTS
    ${VCP}RenderingOpenGL2 ${VCP}GUISupportQt ${VCP}DomainsChemistry
    ${VCP}RenderingVolumeOpenGL2 ${VCP}ViewsCore ${VCP}RenderingFreeType
    ${VCP}ChartsCore ${VCP}ViewsContext2D ${VCP}RenderingContextOpenGL2)
set_property(DIRECTORY APPEND PROPERTY COMPILE_DEFINITIONS ${VTK_DEFINITIONS})

set(HEADERS
  qvtkopenglwidget.h
  vtkAvogadroActor.h
  vtkglwidget.h
  vtkplot.h
)

set(SOURCES
  vtkAvogadroActor.cxx
  vtkglwidget.cpp
  vtkplot.cpp
)

avogadro_add_library(AvogadroVtk ${HEADERS} ${SOURCES})
set_target_properties(AvogadroVtk PROPERTIES AUTOMOC TRUE)
target_include_directories(AvogadroVtk PUBLIC $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>)
target_link_libraries(AvogadroVtk AvogadroRendering AvogadroQtGui
  ${VMP}RenderingOpenGL2 ${VMP}GUISupportQt ${VMP}RenderingVolumeOpenGL2
  ${VMP}RenderingFreeType ${VMP}InteractionStyle ${VMP}ChartsCore ${VMP}ViewsContext2D
  ${VMP}RenderingContextOpenGL2 ${VMP}DomainsChemistryOpenGL2 Qt5::Widgets)
