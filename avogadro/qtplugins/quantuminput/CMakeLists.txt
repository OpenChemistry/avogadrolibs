# Pull in MoleQueue
find_package(MoleQueue REQUIRED NO_MODULE)
include_directories(${MoleQueue_INCLUDE_DIRS})

# Needed to find avogadroioexport.h:
include_directories("${AvogadroLibs_BINARY_DIR}/avogadro/io/")

# Extension
set(quantuminput_srcs
  quantuminput.cpp
)

avogadro_plugin(QuantumInput
  "Quantum input file generation"
  ExtensionPlugin
  quantuminput.h
  QuantumInput
  "${quantuminput_srcs}"
)

target_link_libraries(QuantumInput LINK_PRIVATE AvogadroIO MoleQueueClient)

# Bundled generator scripts
set(input_generators
  inputGenerators/apbs.py
  inputGenerators/gamessuk.py
  inputGenerators/gaussian.py
  inputGenerators/molpro.py
  inputGenerators/mopac.py
  inputGenerators/nwchem.py
  inputGenerators/qchem.py
  inputGenerators/terachem.py
)

# Place input scripts in the same relative path to the build-tree executable
set(configured_script_dir
  "${CMAKE_RUNTIME_OUTPUT_DIRECTORY}/../lib/avogadro2/scripts/inputGenerators/")
message("Writing configured scripts to ${configured_script_dir}")
# Configure files to set shebangs
set(configured_input_generators)
foreach(script ${input_generators})
  get_filename_component(fname "${script}" NAME)
  set(configured_script
    "${configured_script_dir}/${fname}")
  configure_file("${script}" "${configured_script}" @ONLY)
  list(APPEND configured_input_generators "${configured_script}")
endforeach()

# Installed location of the input generators:
set(QUANTUMINPUT_GENERATOR_DIR
  "${INSTALL_LIBRARY_DIR}/avogadro2/scripts/inputGenerators/")

install(PROGRAMS ${configured_input_generators}
  DESTINATION "${QUANTUMINPUT_GENERATOR_DIR}")
